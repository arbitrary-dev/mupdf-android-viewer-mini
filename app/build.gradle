apply plugin: 'com.android.application'

group = 'com.artifex.mupdf'
version = '1.26.10a'

dependencies {
	if (file('../lib/build.gradle').isFile())
		api project(':lib')
	else
		api 'com.artifex.mupdf:mini:1.26.10a'
}

android {
	namespace 'com.artifex.mupdf.mini.app'
	compileSdkVersion 33
	defaultConfig {
		minSdkVersion 21
		// FIXME: `targetSdkVersion 35` needs some additional insets handling
		//        for root view, and it's not pageView, as it may seem.
		//        Otherwise layout gets rendered behind the navigation bar.
		targetSdkVersion 34
		versionName '1.26.10a'
		versionCode 169
	}

	splits {
		abi {
			enable true
			universalApk true
		}
	}

	bundle {
		abi {
			enableSplit true
		}
	}

	if (project.hasProperty('release_storeFile')) {
		signingConfigs {
			release {
				storeFile file(release_storeFile)
				storePassword release_storePassword
				keyAlias release_keyAlias
				keyPassword release_keyPassword
			}
		}
		buildTypes {
			release {
				signingConfig signingConfigs.release
				ndk {
					debugSymbolLevel 'FULL'
				}
			}
		}
	}
}
